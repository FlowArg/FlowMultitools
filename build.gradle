plugins {
    id "com.jfrog.bintray" version "1.8.5"
}

apply plugin: 'java-library'
apply plugin: 'idea'
apply plugin: 'maven-publish'

group 'fr.flowarg'
version '1.2.1'
archivesBaseName = 'flowmultitools'

compileJava {
    sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8
    options.encoding = 'UTF-8'
}

task sourcesJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allSource
}

bintray {
    user = System.getenv('BINTRAY_USERNAME')
    key = System.getenv('BINTRAY_KEY')
    publications = ['mavenJava'] 
    pkg {
        repo = 'maven'
        name = 'FlowMultitools'
        userOrg = 'flowarg'
        licenses = ['GPL-3.0']
        publicDownloadNumbers = true
        vcsUrl = 'https://github.com/FlowArg/FlowMultiools.git'
        version {
            name = 'v' + project.version
            released = new Date()
            vcsTag = 'v' + project.version
        }
    }
}

artifacts {
    archives sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact tasks.sourcesJar
            pom {
                groupId = project.group
                version = project.version
                artifactId = 'flowmultitools'
                name = project.name
                description = 'FlowMultitools is a library for my projects'
                url = 'https://github.com/FlowArg/FlowMultitools'

                issueManagement {
                    system = 'github'
                    url = 'https://github.com/FlowArg/FlowMultitools/issues'
                }

                licenses {
                    license {
                        name = 'LICENSE'
                        url = 'https://github.com/FlowArg/FlowMultitools/blob/master/LICENSE'
                        distribution = 'repo'
                    }
                }
            }
        }
    }
}

repositories {
    jcenter()
}

dependencies {
    implementation 'org.codehaus.plexus:plexus-archiver:2.2'
}
