apply plugin: 'java-library'
apply plugin: 'idea'
apply plugin: 'maven-publish'
apply plugin: 'signing'

group 'fr.flowarg'
version '1.4.4'
archivesBaseName = 'flowmultitools'

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
    withSourcesJar()
    withJavadocJar()
}

dependencies {
    api 'org.apache.commons:commons-compress:1.26.2'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.2'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            
            pom {
                groupId = project.group
                version = project.version
                artifactId = 'flowmultitools'
                name = project.name
                description = 'FlowMultitools is a tool-library for my projects'
                url = 'https://github.com/FlowArg/FlowMultitools'

                scm {
                    connection = 'scm:git:git://github.com/FlowArg/FlowMultitools.git'
                    developerConnection = 'scm:git:ssh://github.com:FlowArg/FlowMultitools.git'
                    url = 'http://github.com/FlowArg/FlowMultitools/tree/master'
                }

                licenses {
                    license {
                        name = 'GNU General Public License v3.0'
                        url = 'https://www.gnu.org/licenses/gpl-3.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'flowarg'
                        name = 'Flow Arg'
                        email = 'flow@flowarg.fr'
                    }
                }
            }
        }
    }
    
    repositories {
        maven {
            credentials {
                username = System.getenv("OSSRH_USERNAME")
                password = System.getenv("OSSRH_PASSWORD")
            }
            url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
        }
    }
}

signing {
    def signingKey = System.getenv("GPG_PRIVATE_KEY")
    def signingPassword = System.getenv("GPG_PASSPHRASE")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.mavenJava
}

test {
    useJUnitPlatform()
}
